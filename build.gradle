plugins {
    id 'java'
    id 'application'
    id 'org.springframework.boot' version '2.6.7'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'com.adarshr.test-logger' version '3.2.0'
}

repositories {
    mavenCentral()
    mavenLocal()
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.adarshr.test-logger'

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-webflux'
        implementation 'org.springframework.boot:spring-boot-starter-security'
        implementation 'org.springframework.boot:spring-boot-starter-webflux'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.kafka:spring-kafka'
        implementation 'org.hibernate.validator:hibernate-validator:6.1.5.Final'
        implementation 'com.h2database:h2'

        implementation 'org.springframework.kafka:spring-kafka-test:2.8.5'
        testImplementation 'org.springframework.kafka:spring-kafka-test:2.8.5'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.springframework.security:spring-security-test'
        testImplementation 'io.projectreactor:reactor-test'
        testImplementation 'org.springframework.kafka:spring-kafka-test'
    }
    task prepareKotlinBuildScriptModel {}
}

allprojects {
    group = 'com.tungstun'
    version = '0.0.1'
    sourceCompatibility = '17'

    def profile = project.hasProperty('profile')
            ? project.property('profile')
            : 'dev'

    test {
        useJUnitPlatform()
        doFirst {
            systemProperty 'spring.profiles.active', profile
        }
    }


    task unittest(type: Test) {
        description("Runs all tests except Application, Integration and Message Tests")
        useJUnitPlatform()
        doFirst {
            systemProperty 'spring.profiles.active', profile
        }
        filter {
            excludeTestsMatching '*ApplicationTests'
            excludeTestsMatching '*IntegrationTest'
            excludeTestsMatching '*MessageTest'
            setFailOnNoMatchingTests(false)
        }
    }
    testlogger {
        theme 'standard'
        showSimpleNames true

        if (project.hasProperty('c') || project.hasProperty('compact')){
            showExceptions false
            showStackTraces false
            showCauses false
            showPassedStandardStreams false
            showSkippedStandardStreams false
            showFailedStandardStreams false
        }else {
            showStandardStreams true
        }
    }
}
